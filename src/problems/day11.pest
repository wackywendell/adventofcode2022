number = {            // To recognize a number...
    ASCII_DIGIT+       //   take as many ASCII digits as possible (at least one).
}

COLON = _{":"}
COMMA_SPACE = _{", "}
SPACE = _{" "}
NEWLINE = _{"\n"}

MONKEY = _{"Monkey "}

monkeyId = {
    MONKEY ~ number ~ COLON
}


STARTING = _{"  Starting items: "}

startingLine = {
    STARTING ~ number ~ (COMMA_SPACE ~ number)*
}

OPERATION_INIT = _{"  Operation: new = old "}
operation = {"*" | "+" | "-" | "/"}
old = {"old"}

operationLine = {
    OPERATION_INIT ~ operation ~ SPACE ~ (number | old)
}


TEST_INIT = _{"  Test: divisible by "}
THROW_TRUE = _{"    If true: throw to monkey "}
THROW_FALSE = _{"    If false: throw to monkey "}

throwLineTrue = {
    THROW_TRUE ~ number
}
throwLineFalse = {
    THROW_FALSE ~ number
}


testLine = {
    TEST_INIT ~ number ~ NEWLINE
    ~ THROW_TRUE ~ number ~ NEWLINE
    ~ THROW_FALSE ~ number
}


Monkey = {
    monkeyId ~ NEWLINE ~ startingLine ~ NEWLINE ~ operationLine ~ NEWLINE ~ testLine
}

Monkeys = {
    SOI ~ Monkey ~ (NEWLINE ~ NEWLINE ~ Monkey)* ~ EOI
}

// Monkey 0:
//   Starting items: 79, 98
//   Operation: new = old * 19
//   Test: divisible by 23
//     If true: throw to monkey 2
//     If false: throw to monkey 3

